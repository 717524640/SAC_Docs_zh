\section{SAC宏\protect\footnotemark}
\label{sec:macros}
\footnotetext{本节的全部内容均可通过脚本实现，建议使用脚本而非SAC宏}
\SACTitle{概述：}
简单的说，将一系列要一起执行的SAC命令放在一个文件中即构成了SAC宏。就像一般命令
以及内置函数一样，SAC宏文件可以包含头段变量以及暂存块变量，在命令被执行之前这
些变量或者表达式的值将首先被计算出来。

\SACTitle{一个简单的例子：}
假设你有一系列要重复执行多次的命令，那么SAC宏文件显然可以帮你节省不少时间。只要
打开你喜欢的文本编辑器，把这些命令放在一个文件中，然后你就可以用命令MACRO来执行
这个文件中的全部命令。假设你要读取重复读取相同的三个文件，文件名分别为ABC、DEF、XYZ，
每一个文件分别乘以不同的值，做Fourier变换，然后将频谱的振幅部分绘制到SGF文件中，
这样的一个宏文件如下所示：
\begin{SACCode}
  ** This certainly is a simple little macro.
  r ABC DEF XYZ
  mul 4 8 9
  fft
  bg sgf
  psp am
\end{SACCode}

假设这个文件命名为MYSTUFF，并且该文件在你的当前目录中，你可以通过下面的命令执行宏文件：
\begin{SACCode}
SAC> macro MYSTUFF
\end{SACCode}
注意到宏文件中的命令在执行时是不会在终端中显示该命令的，可以使用ECHO来根据你的意愿
选择在终端显示哪些东西。另外，在任意一行的第一列若有星号则意味着该行为注释行，
SAC不会不解释该行。

\SACTitle{SAC宏参数：}
SAC宏不只是一堆命令的集合，SAC宏文件也可以有自己的参数。
上面的例子很简单但是不够灵活，如果你想要每次读取不同的文件或者乘以不同的值那么你必须
每次都修改这个文件，让宏文件在执行之前允许用户输入参数可以大大增加宏文件的灵活性。
下面将对先前的宏文件进行修改以使其可以接收文件名作为参数：
\begin{SACCode}
  r $1 $2 $3
  mul 4 8 9
  fft
  bg sgf
  psp am
\end{SACCode}
这些以\$开头的变量代表宏文件接收的参数，其分别代表第一、二、三个参数，用下面的命令
执行这个宏文件：
\begin{SACCode}
SAC> macro MYSTUFF ABC DEF XYZ 
\end{SACCode}
可以用下面的命令再次执行这个宏文件读取不同的文件：
\begin{SACCode}
SAC> macro MYSTUFF AAA BBB CCC
\end{SACCode}

\SACTitle{关键字驱动参数：}
关键字驱动参数允许用户按照任意顺序输入参数，这也使得宏文件的内容变得简单易懂。
当参数的数目以及宏文件的大小不断增大的时候这就变得更加重要了。
下面将再一次修改这个例子以使其可以接受文件列表以及乘数的列表：
\begin{SACCode}
  $keys files values
  r $files
  mul $values
  fft
  bg sgf
  psp am
\end{SACCode}
这样一个简单的修改增加了宏文件的灵活性和可读性。第一行表明有两个关键字，
一个称为``files''，一个称为``values''。可以按照下面的输入来执行这个宏文件：
\begin{SACCode}
SAC> macro MYSTUFF FILES ABC DEF XYZ VALUES 4 8 9
\end{SACCode}
因为参数的顺序不再重要，所以你可以像下面这样输入：
\begin{SACCode}
SAC> MACRO MYSTUFF VALUES 4 8 9 FILES ABC DEF XYZ
\end{SACCode}
这个宏文件并不限于读取三个文件，它对于文件的数目没有限制，只要文件数与值数目相匹配就好。

\SACTitle{默认参数值：}
有些时候会遇到这样的情况，宏文件的有些参数在多次执行的过程中经常但并不总是拥有相同的值。
为这些参数提供缺省值可以减少输入那些相同值的次数同时又保有宏参数本身的灵活性。如下例所示：
\begin{SACCode}
  $keys files values
  $default values 4 8 9
  r $files
  mul $values
  fft
  bg sgf
  psp am
\end{SACCode}
在宏文件的第二行指定了这些参数的缺省值，如果在执行宏文件时不输入VALUES的参数值那么这些
参数将使用缺省值：
\begin{SACCode}
SAC> macro MYSTUFF FILES ABC DEF XYZ 
\end{SACCode}
如果想要使用不同的值，可以像下面这样输入：
\begin{SACCode}
SAC> macro MYSTUFF VALUES 10 12 3 FILES ABC DEF XYZ
\end{SACCode}

\SACTitle{参数请求：}
如果你在执行宏文件时没有输入参数而这些参数又没有缺省值，SAC 将会在终端中提示你输入相应的
参数。在上面的例子中，如果你忘记输入参数则会出现下面的情况：
\begin{SACCode}
SAC> MACRO MYSTUFF
FILES?
SAC> ABC DEF XYZ 
\end{SACCode}
注意到SAC 并不会提示输入VALUES的值，因为它们已经有了缺省值。SAC并非在一开始就提示输入
参数，其等到需要计算参数值却发现没有缺省值或者输入值时才会提示需要输入该参数。

\SACTitle{SAC宏中的暂存块变量：}
下面看一看暂存块变量在宏文件中如何使用。在接下来的例子中，第一值是一个变量，其它值是根据
第一个值计算出来的：
\begin{SACCode}
  $KEYS FILES VALUE1
  $DEFAULT VALUE1 4
  READ $FILES
  EVALUATE TO VALUE2 $VALUE1 * 2
  EVALUATE TO VALUE3 %VALUE2 + 1
  MUL $VALUE1 %VALUE2 %VALUE3
  FFT
  BG SGF
  PSP AM 
\end{SACCode}
按照下面的命令执行宏文件：
\begin{SACCode}
SAC> macro MYSTUFF VALUE1 6 FILES ABC DEF XYZ 
\end{SACCode}

\SACTitle{联接：}
你可以在宏参数的前后加上任意的文本、暂存块变量、头段变量。想要在参数前面添加文本的
话就直接在参数前加上需要的字符串即可（这样不会有任何的歧义，因为变量与新增字符之间
是有一个\%或者\$隔开的）；若是想在参数之后添加字符串，那么必须在该参数后、字符串前
重复分割符（\$、\%或者\&，请回顾一下这些分割符分别对应什么样的变量）。如下的的例子所示：

假设宏参数STATION值为ABC，那么\$STATION\$.Z的值就是ABC.Z

假设暂存块变量TEMP值为ABC那么XYZ\%TEMP的值就是XYZABC，而\%TEMP\%XYZ的值为ABCXYZ

假设文件Z的头段变量KA值为IPU0那么(\&Z,KA\&)的值就是(IPU0)

\SACTitle{嵌套与递归：}
SAC提供嵌套功能，不支持递归，但是SAC并不会去检查宏的调用是否保证不是递归，因而需要
用户去保证宏文件不要直接或间接调用自己。

\SACTitle{中断宏：}
有些时候需要临时中断宏文件的执行，用户自己从终端输入一些命令，然后继续执行宏文件。
这个可以利用SAC的pause和resume特性做到。当SAC在宏文件中遇到\$TERMINAL时它会临时停止
执行宏文件，更改提示符为宏名，然后提示从终端输入命令，然后当SAC在终端中看到\$RESUME
时则会停止从终端读取命令继续从宏文件读取。如果你不想再继续执行宏文件中的命令，可以
在终端输入\$KILL，SAC将关闭宏文件，回到上一层。在一个宏文件中可以有多个\$TERMINAL中断。

\SACTitle{If Tests：}
这个特性让你能够在宏文件中修改命令的执行顺序，其语法与fortran77很相似但是又不完全
相同，要注意区分。
\begin{SACCode}
  IF expr
  	commands
  ELSEIF expr
  	commands
  ELSE
  	commands
  ENDIF
\end{SACCode}
在上面的语句中expr是一个如下形式的逻辑表达式：
\begin{SACCode}
           token 操作符 token
\end{SACCode}
其中token可以是一个常数、宏参数、暂存块变量或者头段变量，操作符则是GT、GE、LE、LT、EQ、NE
中的一个。上面的逻辑表达式在计算之前token会被转换为浮点型数。if语句目前最多允许嵌套10次，
elseif、else是可选的，elseif的次数没有限制。

注意这里的if语句与fortran77中if语句的区别：
在逻辑表达式两边没有小括号，并且没有关键字then！下面给出一个例子：
\begin{SACCode}
  READ $1
  MARKPTP
  IF &1,USER0 GE 2.45
    FFT
    PLOTSP AM
  ELSE
    MESSAGE "Peak to peak for $1 below threshold."
  ENDIF
\end{SACCode}
在这个例子中，一个文件被读入内存，测出其最大峰峰值，如果这个值大于某一确定值，
则对其做Fourier变换并绘制振幅图，否则则输出信息到终端。

\SACTitle{Do Loops：}
这个特性允许你重复执行一系列命令。可以通过固定循环次数、遍历元素列表或者设定一定条件
来执行一定命令，你也可以中途中断一次循环。循环的最大嵌套次数为10次。其语法可以有多种形式：
\begin{SACCode}
  DO variable = start, stop, {,increment}
  	commands
  ENDDO

  DO variable FROM start TO stop { BY increment}
  	commands
  ENDDO

  DO variable LIST entrylist
  	commands
  ENDDO

  DO variable WILD {DIR name} entrylist
  	commands
  ENDDO

  WHILE expr
  	commands
  ENDDO
  BREAK
\end{SACCode}
其中大写字符串均为关键字，不可更改：
\begin{itemize}
\renewcommand\labelitemi{\dag}
\item variable是循环变量名，在变量名前加上\$即可以在do循环中使用该变量
\item start、stop、increment 循环变量的初值、终值、增值，start、stop必须为整型数，
		increment缺省值为1
\item entrylist 是do循环执行时变量可以取的所有值的集合，值之间以空格分开，其可以为整型、
	浮点型或字符型。DO WILD中entrylist由字符串和通配符构成，循环执行前，这个列表将根据
	通配符扩展为一系列文件名。
\end{itemize}

\SACTitle{Do循环例子：}
第一个宏文件对文件使用了DIF命令以预白化数据，施加Fourier变换，然后使用DIVOMEGA命令去除
预白化的影响，有时需要在做变换之前多次预白化，那么就可以这样写：
\begin{SACCode}
  $KEYS FILE NPREW
  $DEFAULT NPREW 1
  READ $FILE
  DO J = 1 , $NPREW
  	DIF
  ENDDO
  FFT AMPH
  DO J = 1 , $NPREW
  	DIVOMEGA
  ENDDO
\end{SACCode}
下面这个例子，用相同的数据绘制5个不同的两秒时间窗的质点运动矢量图：
\begin{SACCode}
  READ ABC
  SETBB TIME1 0
  DO TIME2 FROM 2 TO 10 BY 2
  	XLIM %TIME1 $TIME2
  	TITLE 'Particle Motion from %TIME1 to $TIME2$'
  	PLOTPM
  	SETBB TIME1 $TIME2
  ENDDO 
\end{SACCode}
想一想为什么在TITLE命令中为什么TIME2的后面也需要一个\$？
在下面的例子中，一个宏文件调用另一个名为PREVIEW的宏文件，通过do循环以达到多次调用PREVIEW的目的：
\begin{SACCode}
  DO STATION LIST ABC DEF XYZ
  	DO COMPONENT LIST Z N E
  		MACRO PREVIEW $STATION$.$COMPONENT$
 	ENDDO
  ENDDO
\end{SACCode}
在下面的例子中我们修改上一个宏文件使得其可以处理目录MYDIR中所有以".Z"结束的文件：
\begin{SACCode}
  DO FILE WILD DIR MYDIR *.Z
 	MACRO PREVIEW $FILE
  ENDDO 
\end{SACCode}
最后一个例子有三个参数,第一个是文件名，第二个是一个常数，第三个是一个阀值。宏文件读取了
一个数据文件，然后每个数据点乘以一个常数直到其超过某一阀值：
\begin{SACCode}
  READ $1
  WHILE &1,DEPMAX GT $3
  	MUL $2
  ENDDO 
\end{SACCode}
另一个与BREAK有关的宏文件：
\begin{SACCode}
  READ $1
  WHILE 1 GT 0
  	DIV $2
  	IF &1,DEPMAX GT $3
  	BREAK
  	ENDIF
  ENDDO 
\end{SACCode}
这个WHILE循环是一个无限循环，它只能通过BREAK来中断。(这个版本有一个bug，如果初始的最大值
已经低于阀值了，那么BREAK就永远不会被执行)

\SACTitle{从宏文件中执行其他程序：}
你可以在SAC宏内部执行其他程序，可以向程序传递一个可选的执行信息。如果程序是交互式的
你也可以将输入行发送给它，语法如下：
\begin{SACCode}
  $RUN program message
  inputlines
  ENDRUN
\end{SACCode}
宏参数、暂存块变量、头段变量、内联函数均可使用，在程序执行之前它们会被计算，当程序执行
结束，SAC宏会在ENDRUN之后继续执行。

\SACTitle{宏搜索路径：}
当你执行一个宏而又没有给出宏文件的路径时,SAC会按照下面的顺序搜索宏文件：
\begin{itemize}
\renewcommand\labelitemi{\dag}
\item 在当前目录搜索
\item 在SETMACRO命令设置的搜索目录中搜索
\item 在SAC的全局宏目录中搜索
\end{itemize}

全局宏目录包含了对系统上的所有人都可以使用的宏文件，这个全局宏目录一般是sac/aux/macros。
可以使用INSTALLMACRO命令将自己的宏文件安装到这个目录中。你也可以通过绝对或相对路径来专门
指定搜索路径。

\SACTitle{转义字符：}
有些时候可能会需要在命令中使用\$或者\%而又不想SAC将其解释为一个变量。为了实现这个目的可以
在这些特殊的字符之前加上另一个特殊的字符，称为转义符，SAC中的转义符为@，可以被转义的特殊
符号包括：
\begin{itemize}
\renewcommand\labelitemi{\dag}
\item \$  宏参数扩展字符
\item \%  暂存块变量扩增字符
\item \&  头段变量扩展字符
\item  @  转义字符本身
\item  (  内置函数起始符
\item  )  内置函数结束符
\end{itemize}
